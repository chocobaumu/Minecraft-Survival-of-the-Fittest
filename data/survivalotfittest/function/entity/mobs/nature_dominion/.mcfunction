scoreboard players add @s Choco.SotF.SneakTick 0
tag @s add Choco.SotF.Illigers
scoreboard players add @s Choco.SotF.ScoreStorage.3 0
effect clear @s poison

scoreboard players add @s Choco.SotF.Tick.0 1

#bossbar
bossbar add sotf:nature_dominion [{"bold":true,"color":"green","italic":false,"text":"‚Äï‚Äï‚â∂üçÉ‚â∑"},{"bold":true,"color":"#42612F","italic":false,"text":"„Éç„Ç§„ÉÅ„É£„Éº„Éâ„Éü„Éã„Ç™„É≥"},{"bold":true,"color":"green","italic":false,"text":"‚â∂üçÉ‚â∑‚Äï‚Äï"}]
execute unless entity @s[tag=Choco.SotF.Phase2] run bossbar set sotf:nature_dominion color green
execute if entity @s[tag=Choco.SotF.Phase2] run bossbar set sotf:nature_dominion color red
bossbar set sotf:nature_dominion max 300
bossbar set sotf:nature_dominion visible true
bossbar set sotf:nature_dominion style notched_20
bossbar set sotf:nature_dominion players @a[distance=..48]
execute store result bossbar sotf:nature_dominion value run data get entity @s Health
execute unless entity @s[tag=Choco.SotF.Phase2] run bossbar set sotf:nature_dominion name [{"bold":true,"color":"green","italic":false,"text":"‚Äï‚Äï‚â∂üçÉ‚â∑"},{"bold":true,"color":"#42612F","italic":false,"text":"„Éç„Ç§„ÉÅ„É£„Éº„Éâ„Éü„Éã„Ç™„É≥"},{"bold":true,"color":"green","italic":false,"text":"‚â∂üçÉ‚â∑‚Äï‚Äï"}]
execute if entity @s[tag=Choco.SotF.Phase2] run bossbar set sotf:nature_dominion name [{"bold":true,"color":"red","italic":false,"text":"‚Äï‚Äï‚â∂üçÉ‚â∑"},{"bold":true,"color":"#42612F","italic":false,"text":"„Éç„Ç§„ÉÅ„É£„Éº„Éâ„Éü„Éã„Ç™„É≥"},{"bold":true,"color":"red","italic":false,"text":"‚â∂üçÉ‚â∑‚Äï‚Äï"}]


# HealthÂèñÂæó
execute store result score @s Choco.SotF.Health run data get entity @s Health

# „Éà„Éº„ÉÜ„É†Êï∞ÂèñÂæó
scoreboard players set @s Choco.SotF.ScoreStorage.6 0
execute if items entity @s weapon.mainhand totem_of_undying store result score @s Choco.SotF.ScoreStorage.6 run data get entity @s equipment.mainhand.count

# PosÂèñÂæó
execute store result score @s Choco.SotF.Pos.0 run data get entity @s Pos[0]
execute store result score @s Choco.SotF.Pos.1 run data get entity @s Pos[1]
execute store result score @s Choco.SotF.Pos.2 run data get entity @s Pos[2]

# HP„Å´„Çà„Çã„É©„Éô„Ç∏„É£„ÉºÂè¨Âñö
execute if score @s Choco.SotF.Health matches ..225 if score @s Choco.SotF.tick.5 matches 0 unless entity @s[tag=Choco.SotF.RavagerSummon.1] run scoreboard players set @s Choco.SotF.tick.5 1
execute if score @s Choco.SotF.Health matches ..150 if score @s Choco.SotF.tick.5 matches 0 unless entity @s[tag=Choco.SotF.RavagerSummon.2] run scoreboard players set @s Choco.SotF.tick.5 1
execute if score @s Choco.SotF.Health matches ..75 if score @s Choco.SotF.tick.5 matches 0 unless entity @s[tag=Choco.SotF.RavagerSummon.3] run scoreboard players set @s Choco.SotF.tick.5 1

# HurtTimeÂèñÂæó
execute store result score @s Choco.SotF.Data.HurtTime run data get entity @s HurtTime
execute if score @s Choco.SotF.Data.HurtTime matches 9 at @s run function survivalotfittest:entity/mobs/nature_dominion/hurt with entity @s

# InvulnerableÂèñÂæó
execute store result score @s Choco.SotF.ScoreStorage.0 run data get entity @s Invulnerable

# ‰Ωï„Åã„Å´‰πó„Å£„Å¶„ÅÑ„ÇãÂ†¥Âêà
scoreboard players set @s Choco.SotF.ScoreStorage.1 0
execute as @s on vehicle as @s on passengers run scoreboard players set @s Choco.SotF.ScoreStorage.1 1

execute if score @s Choco.SotF.ScoreStorage.0 matches 0 if score @s Choco.SotF.ScoreStorage.1 matches 1 run data merge entity @s {Invulnerable:1b}
execute if score @s Choco.SotF.ScoreStorage.0 matches 1 if score @s Choco.SotF.ScoreStorage.1 matches 0 run data merge entity @s {Invulnerable:0b}

# Target
execute as @s on target as @s run tag @s add Choco.SotF.Target

execute if score @s Choco.SotF.Tick.0 matches 20.. if entity @e[tag=Choco.SotF.Target] at @s run function survivalotfittest:entity/mobs/nature_dominion/fight/

execute if score @s Choco.SotF.SneakTick matches 1.. run scoreboard players remove @s Choco.SotF.SneakTick 1

# È≠îË°ì(ÊïµÂØæÈñ¢‰øÇ„Å™„ÅÑ„É§„ÉÑ)

# > „Çπ„Éà„Éº„É≥„Éª„Ç∑„Éº„É´„Éâ
execute at @s run function survivalotfittest:entity/mobs/nature_dominion/sorcery/stone_shield/

# > „Çµ„É¢„É≥„Éª„É©„É¥„Çß„Ç∏„É£„Éº
scoreboard players add @s Choco.SotF.tick.5 0
execute if score @s Choco.SotF.ScoreStorage.1 matches 0 if score @s Choco.SotF.tick.5 matches 0 if score @s Choco.SotF.Health matches ..75 run scoreboard players add @s Choco.SotF.tick.5 1
execute if score @s Choco.SotF.tick.5 matches 1.. at @s run function survivalotfittest:entity/mobs/nature_dominion/sorcery/summon_ravager/

# > „ÇΩ„Ç¶„É´„Éª„Ç®„ÇØ„Çπ„Éó„É≠„Éº„Ç∏„Éß„É≥
execute at @s store result score @s Choco.SotF.ScoreStorage.9 run execute if entity @e[type=vex,tag=!Choco.SotF.Custom,distance=..64]
execute at @s if score @s Choco.SotF.ScoreStorage.9 matches 10.. run function survivalotfittest:entity/mobs/nature_dominion/sorcery/soul_explosion/

# „Éà„Éº„ÉÜ„É†Êï∞ÊØîËºÉ
scoreboard players operation @s Choco.SotF.ScoreStorage.7 -= @s Choco.SotF.ScoreStorage.6

# „Éà„Éº„ÉÜ„É†„Çí‰Ωø„Å£„Åü„Å®„Åç
scoreboard players add @s Choco.SotF.tick.11 0
execute unless score @s Choco.SotF.Tick.0 matches ..3 unless score @s Choco.SotF.ScoreStorage.7 matches 0 at @s run function survivalotfittest:entity/mobs/nature_dominion/totem_used/

scoreboard players operation @s Choco.SotF.ScoreStorage.7 = @s Choco.SotF.ScoreStorage.6

# ÂõûÂæ©„Éù„Éº„Ç∑„Éß„É≥
execute if score @s Choco.SotF.tick.11 matches 1.. at @s run function survivalotfittest:entity/mobs/nature_dominion/totem_used/potion

# „Éï„Çß„Éº„Ç∫2
execute if entity @s[tag=Choco.SotF.Phase2] at @s run function survivalotfittest:entity/mobs/nature_dominion/phase2/

tag @e[tag=Choco.SotF.Target] remove Choco.SotF.Target
